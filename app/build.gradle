apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.ben-manes.versions'

apply from: 'dependencies.gradle'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.nerevar.notes"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"
    productFlavors {
        dev {
            applicationId "com.nerevar.notes.dev"
            resValue "string", "app_name", "Notes dev"
            dimension "default"
        }

        production {
            resValue "string", "app_name", "Notes"
            dimension "default"
        }
    }
}

dependencyUpdates {
    resolutionStrategy = {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }
}

dependencies {
    implementation kotlinDeps.values()
    implementation diDeps.values()
    implementation androidxDeps.values()
    implementation restDeps.values()
}
